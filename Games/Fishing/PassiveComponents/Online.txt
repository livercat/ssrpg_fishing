var utils = import Games/Fishing/Utils
var progress = import Games/Fishing/Progress
var topHud = import Games/Fishing/FishingComponents/HUD
var flyup = new Games/FlyupText
var styles = import Games/Fishing/Assets/Styles

var root = null
var hirelingAnim = null
var label = null

var state = null
var timer = null

var lastCollectTime = null
var collectedScales = 0.0

var hireling = null

func MakeActiveHire(level, activeRoot)
  ?root ! null
    return

  // Create hireling
  var hirelingFactory = new Games/Fishing/Hireling
  hireling = hirelingFactory.Make(level)

  // Create root 
  root = ui.AddPanel()
  activeRoot.Add(root)
  root.style = styles.invisStyle
  root.dock = top_center
  root.anchor = top_center
  root.w = 1
  root.h = 1

  // Create label
  label = ui.AddText()
  activeRoot.Add(label)
  label.anchor = top_left
  label.color = #888888
  label.text = te.xt("x"+progress.hirelings[level])

  // From hireling level: set xy (anim & label), create new copy of anim
  ?level >= 4
    root.x = -22 - 5*(level-4)
    root.y = 5
    label.x = root.x + 3
    label.y = root.y + 3
    hirelingAnim = new Games/Fishing/PassiveComponents/HirelingSeated
  :?level >= 1
    root.x = -10 - 10*(level-1)
    root.y = 20 + 2*((level-1)%2)
    label.x = root.x + 5
    label.y = root.y
    hirelingAnim = new Games/Fishing/PassiveComponents/HirelingCanoe
  :
    root.x = 2
    root.y = 16
    label.x = root.x + 2
    label.y = root.y + 3
    hirelingAnim = new Games/Fishing/PassiveComponents/HirelingFall

  // Pass hireling root and color to animation
  hirelingAnim.root = root
  hirelingAnim.animColor = hireling.color
  // Init hireling animation
  hirelingAnim.InitActive()

func UpdateTimer()
  // Update catch timer with rng from hireling level
  var min = hireling.GetMinWaitTime()
  var max = hireling.GetMaxWaitTime()
  var n = utils.RngRange(min, max)
  timer = time + n

func CollectScales()
  play cross_deadwood_row
  // Calculate scales collected since last collection
  collectedScales = collectedScales + hireling.GetScalesPerSecond() * ((time - lastCollectTime) / 30.0)
  lastCollectTime = time
  var scales = math.FloorToInt(collectedScales)
  collectedScales = collectedScales - scales // remainder
  // Create flyup for scales gained
  flyup.Show(root.absoluteX-3, root.absoluteY, string.Format(te.xt(tid_fish_scales), scales))
  // Update progress & HUD with scales
  progress.AddScales(scales)
  topHud.Update()

func Update(gameState)
  // Initial state "null"
  ?state = null
    // Setup timer for catching
    UpdateTimer()
    // Recycle any potential NewHire animations
    hirelingAnim.RecycleNewHire()
    // Set initial lastCollectTime
    lastCollectTime = time
    // Enter waiting state to begin active loop
    state = waiting
  :?state = waiting
    // Wait for catch timer to be reached
    ?time >= timer
      // Start hireling's catch animation
      hirelingAnim.PlayActive()
      // Add 1sec to timer for catch part 1
      state = catch1
      timer = time + 30
  :?state = catch1
    // Wait for catch p1 to complete
    ?time >= timer
      // Process flyup and scales
      CollectScales()
      // Add another sec for catch part 2
      state = catch2
      timer = time + 30
  :?state = catch2
    // Check if shop is open
    ?gameState ! Shop
      // Update flyup during catch p2
      flyup.Update()
    // Wait for catch p2 to complete
    ?time >= timer
      // Reset hireling's catch animation
      hirelingAnim.ResetActive()
      // Reset timer for next catch & re-enter active loop
      UpdateTimer()
      state = waiting
  // Special state "newhire" given by passive.txt when hireling 
  // was just purchased from shop, then wait for shop to be closed 
  :?state = newhire & gameState ! Shop
    // Call animation's NewHire update loop
    hirelingAnim.NewHireUpdate()
    // Check for NewHire animation to complete
    ?hirelingAnim.state = done
      // Enter active loop
      state = null
  