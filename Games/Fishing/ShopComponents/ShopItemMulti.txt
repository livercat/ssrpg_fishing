// An item that can be purchased multiple times

var utils = import Games/Fishing/Utils
var _ui = import Games/Fishing/ShopComponents/UIFactory

var panel = null
var button = null
var icon = null
var amountLabel = null
var costLabel = null

var baseItem = null

func ToString()
  return "ShopItemMulti: " + baseItem

func Init(baseItem, handler, parent)
  this.baseItem = baseItem

  panel = _ui.MakeItemPanel(baseItem.level * 8)
  parent.Add(panel)
  
  button = _ui.MakeItemButton(handler)
  panel.Add(button)

  costLabel = _ui.MakeItemLabel(-1)
  panel.Add(costLabel)

  amountLabel = _ui.MakeItemLabel(-2)
  panel.Add(amountLabel)
  
  icon = _ui.MakeItemIcon(baseItem.sprite, baseItem.color)
  panel.Add(icon)

  Update()

  return this

func Update()
  amountLabel.text = baseItem.GetOwnedAmount() + "/" + baseItem.maxAmount
  ?baseItem.GetOwnedAmount() >= baseItem.maxAmount
    amountLabel.color = _ui.inactiveColor
    costLabel.text = te.xt("-MAX-")
    costLabel.color = _ui.inactiveColor
  :
    amountLabel.color = _ui.activeColor
    costLabel.text = utils.FormatCost(baseItem.RealCost())
    costLabel.color = _ui.activeColor
  
func Hide()
  button.visible = false
  costLabel.visible = false
  amountLabel.visible = false
  icon.visible = false

func Reveal()
  button.visible = inherit
  costLabel.visible = inherit
  amountLabel.visible = inherit
  icon.visible = inherit